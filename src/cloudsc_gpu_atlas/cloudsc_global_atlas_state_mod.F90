! (C) Copyright 1988- ECMWF.
!
! This software is licensed under the terms of the Apache Licence Version 2.0
! which can be obtained at http://www.apache.org/licenses/LICENSE-2.0.
!
! In applying this licence, ECMWF does not waive the privileges and immunities
! granted to it by virtue of its status as an intergovernmental organisation
! nor does it submit to any jurisdiction.

MODULE CLOUDSC_GLOBAL_ATLAS_STATE_MOD
  ! Driver module to manage the setup and teardown of the dwarf memory state
  USE PARKIND1,  ONLY : JPIM, JPRB
  USE YOMPHYDER, ONLY : STATE_TYPE
  USE YOECLDP,   ONLY : NCLV, YRECLDP, YRECLDP_LOAD_PARAMETERS
  USE YOMCST,    ONLY : YOMCST_LOAD_PARAMETERS
  USE YOETHF,    ONLY : YOETHF_LOAD_PARAMETERS
  USE YOEPHLI  , ONLY : YREPHLI, YREPHLI_LOAD_PARAMETERS
  USE TIMER_MOD, ONLY : PERFORMANCE_TIMER, GET_THREAD_NUM, FTIMER

  USE FILE_IO_MOD, ONLY: INPUT_INITIALIZE, INPUT_FINALIZE, LOAD_SCALAR, LOAD_ARRAY
  USE EXPAND_ATLAS_MOD, ONLY: LOADVAR_ATLAS
  USE VALIDATE_ATLAS_MOD, ONLY: VALIDATEVAR_ATLAS
  USE CLOUDSC_MPI_MOD, ONLY: IRANK

  USE ATLAS_MODULE
  USE ATLAS_MULTIFIELD_MODULE, ONLY : ATLAS_MULTIFIELD
  USE, INTRINSIC :: ISO_C_BINDING

  IMPLICIT NONE

  TYPE VAR3D_PTR
      REAL(KIND=JPRB), POINTER :: PTR(:,:,:)
  END TYPE
  TYPE VAR2D_PTR
      REAL(KIND=JPRB), POINTER :: PTR(:,:)
  END TYPE

  CHARACTER(LEN=16), PARAMETER, DIMENSION(37) :: IN_VAR_NAMES = (/ &
      "PLCRIT_AER      ", "PICRIT_AER      ", "PRE_ICE         ", "PCCN            ", "PNICE           ", &
      "PT              ", "PQ              ", "PVFA            ", "PVFL            ", "PVFI            ", &
      "PDYNA           ", "PDYNL           ", "PDYNI           ", "PHRSW           ", "PHRLW           ", &
      "PVERVEL         ", "PAP             ", "PLU             ", "PLUDE           ", "PSNDE           ", &
      "PMFU            ", "PMFD            ", "PA              ", "PSUPSAT         ", "TENDENCY_CML_T  ", &
      "TENDENCY_CML_A  ", "TENDENCY_CML_Q  ", "TENDENCY_TMP_T  ", "TENDENCY_TMP_A  ", "TENDENCY_TMP_Q  ", &
      "PLSM            ", "LDCUM           ", "KTYPE           ", "PAPH            ", "PCLV            ", &
      "TENDENCY_CML_CLD", "TENDENCY_TMP_CLD" /)
  CHARACTER(LEN=16), PARAMETER, DIMENSION(20) :: OUT_VAR_NAMES = (/ &
      "PFSQLF          ", "PFSQIF          ", "PFCQLNG         ", "PFCQNNG         ", "PFSQRF          ", &
      "PFSQSF          ", "PFCQRNG         ", "PFCQSNG         ", "PFSQLTUR        ", "PFSQITUR        ", &
      "PFPLSL          ", "PFPLSN          ", "PFHPSL          ", "PFHPSN          ", "PCOVPTOT        ", &
      "TENDENCY_LOC_T  ", "TENDENCY_LOC_A  ", "TENDENCY_LOC_Q  ", "PRAINFRAC_TOPRFZ", "TENDENCY_LOC_CLD" /) 

  TYPE CLOUDSC_GLOBAL_ATLAS_STATE
    ! Memory state containing raw fields annd tendencies for CLOUDSC dwarf
    !
    ! Note that the global state has an additional outermost block
    ! dimension allocated for each field variable.
    INTEGER(KIND=JPIM)                   :: NPROMA, KLEV    ! Grid points and vertical levels per block
    INTEGER(KIND=JPIM)                   :: NGPTOT, NBLOCKS ! Total number of grid points and blocks
    INTEGER(KIND=JPIM)                   :: KFLDX 
    LOGICAL                              :: LDSLPHY 
    LOGICAL                              :: LDMAINCALL      ! T if main call to cloudsc
    REAL(KIND=JPRB)                      :: PTSPHY          ! Physics timestep

  CONTAINS
    PROCEDURE :: LOAD => CLOUDSC_GLOBAL_ATLAS_STATE_LOAD
    PROCEDURE :: VALIDATE => CLOUDSC_GLOBAL_ATLAS_STATE_VALIDATE

  END TYPE CLOUDSC_GLOBAL_ATLAS_STATE

  PUBLIC :: OUT_VAR_NAMES, IN_VAR_NAMES

CONTAINS


  SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_LOAD(SELF, FSET, FSPACE, NPROMA, NGPTOTG)
    ! Load reference input data via serialbox
    CLASS(CLOUDSC_GLOBAL_ATLAS_STATE) :: SELF
    TYPE(ATLAS_FIELDSET), INTENT(INOUT) :: FSET
    TYPE(ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS), INTENT(INOUT) :: FSPACE
    INTEGER(KIND=JPIM), INTENT(IN) :: NPROMA, NGPTOTG

    TYPE(ATLAS_STRUCTUREDGRID) :: GRID
    TYPE(ATLAS_MULTIFIELD) :: MULTIFIELD_IN, MULTIFIELD_OUT
    INTEGER(KIND=JPIM) :: KLON, IVAR, B
    REAL(KIND=JPRB), POINTER :: TMP4D(:,:,:,:)
    REAL(KIND=JPRB), POINTER :: TMP3D(:,:,:)
    REAL(KIND=JPRB), POINTER :: TMP2D(:,:)
    TYPE(ATLAS_FIELD) :: FIELD
    CHARACTER(len=8) :: CENV, FPREC
    INTEGER :: CENV_LEN
    TYPE(ATLAS_TRACE) :: TRACE, TRACE_IO
    INTEGER(KIND=JPIM) :: TID, NGPTOT
    REAL :: TDIFF
    LOGICAL :: LMULTIFIELD = .FALSE.
    INTEGER, PARAMETER :: NUM_IN_MFIELDS = 30
    INTEGER, PARAMETER :: NUM_OUT_MFIELDS = 14

    TRACE = ATLAS_TRACE("cloudsc_global_atlas_state_mod.F90", __LINE__, &
        & "CLOUDSC_GLOBAL_ATLAS_STATE_LOAD")

    CALL GET_ENVIRONMENT_VARIABLE("CLOUDSC_ATLAS_MULTIFIELD", CENV, CENV_LEN)
    IF (CENV_LEN > 0 ) THEN
      IF (TRIM(CENV) == "1") THEN
        LMULTIFIELD = .TRUE.
      ELSE IF (TRIM(CENV) /= "0") THEN
        PRINT *, "LMULTIFIELD can only be 0 or 1."
        STOP
      ENDIF
    ENDIF

    CALL INPUT_INITIALIZE(NAME='input')

    CALL LOAD_SCALAR('KLON', KLON)
    CALL LOAD_SCALAR('KLEV', SELF%KLEV)
    ! CALL LOAD_SCALAR('KFLDX', SELF%KFLDX)

    GRID = ATLAS_REGULARLONLATGRID(NGPTOTG, 1)
    FSPACE = ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS(GRID, LEVELS=SELF%KLEV, NPROMA=NPROMA, HALO=0)
    SELF%NBLOCKS = FSPACE%NBLKS()
    NGPTOT = FSPACE%SIZE()

    IF (IRANK == 0) THEN
      WRITE(0,*) "LMULTIFIELD = ", LMULTIFIELD
      WRITE(0,*) "NGPTOTG     = ", NGPTOTG
      WRITE(0,*) "NGPTOT      = ", NGPTOT
      WRITE(0,*) "NBLKS       = ", FSPACE%NBLKS()
      WRITE(0,*) "NPROMA      = ", NPROMA
    ENDIF

    TDIFF = FTIMER()

    ! input multifield/fieldset
    IF (LMULTIFIELD) THEN
      MULTIFIELD_IN = atlas_MultiField(ATLAS_REAL(JPRB), [NPROMA, FSPACE%LEVELS(), -1, FSPACE%NBLKS()], IN_VAR_NAMES(1:NUM_IN_MFIELDS))
      CALL FSET%ADD(MULTIFIELD_IN%FIELDSET()) 
      CALL MULTIFIELD_IN%FINAL()
    ELSE
      DO IVAR = 1, NUM_IN_MFIELDS
          CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME=TRIM(IN_VAR_NAMES(IVAR)),   KIND=ATLAS_REAL(JPRB)))
          FIELD = FSET%FIELD(TRIM(IN_VAR_NAMES(IVAR)))
      END DO
    ENDIF

    ! special input variables
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PLSM",   KIND=ATLAS_REAL(JPRB),    LEVELS=0))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="LDCUM",  KIND=ATLAS_LOGICAL(),     LEVELS=0))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="KTYPE",  KIND=ATLAS_INTEGER(JPIM), LEVELS=0))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PAPH",   KIND=ATLAS_REAL(JPRB),    LEVELS=FSPACE%LEVELS()+1))
    !CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PEXTRA", KIND=ATLAS_REAL(JPRB),    VARIABLES=MAX(1,SELF%KFLDX)))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PCLV",   KIND=ATLAS_REAL(JPRB),    VARIABLES=MAX(1,NCLV)))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME='TENDENCY_CML_CLD', KIND=ATLAS_REAL(JPRB), VARIABLES=NCLV))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME='TENDENCY_TMP_CLD', KIND=ATLAS_REAL(JPRB), VARIABLES=NCLV))

    ! output multifield/fieldset
    IF (LMULTIFIELD) THEN
        MULTIFIELD_OUT = atlas_MultiField(ATLAS_REAL(JPRB), [NPROMA, FSPACE%LEVELS()+1, -1, FSPACE%NBLKS()], OUT_VAR_NAMES(1:NUM_OUT_MFIELDS))
        CALL FSET%ADD(MULTIFIELD_OUT%FIELDSET()) 
        CALL MULTIFIELD_OUT%FINAL()
    ELSE
        DO IVAR = 1, NUM_OUT_MFIELDS
            CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME=TRIM(OUT_VAR_NAMES(IVAR)), KIND=ATLAS_REAL(JPRB), LEVELS=FSPACE%LEVELS()+1))
        END DO
    ENDIF

    ! special output variables
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PCOVPTOT", KIND=ATLAS_REAL(JPRB)))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME="PRAINFRAC_TOPRFZ", KIND=ATLAS_REAL(JPRB), LEVELS=0))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME='TENDENCY_LOC_T', KIND=ATLAS_REAL(JPRB)))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME='TENDENCY_LOC_A', KIND=ATLAS_REAL(JPRB)))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME='TENDENCY_LOC_Q', KIND=ATLAS_REAL(JPRB)))
    CALL FSET%ADD(FSPACE%CREATE_FIELD(NAME='TENDENCY_LOC_CLD', KIND=ATLAS_REAL(JPRB), VARIABLES=NCLV))

    TDIFF = FTIMER() - TDIFF
    PRINT *, "Host allocate [ms]: ", INT(TDIFF * 1000.0)

    DO IVAR = 1, SIZE(IN_VAR_NAMES)
        CALL LOADVAR_ATLAS(FSET, FSPACE, TRIM(IN_VAR_NAMES(IVAR)), KLON, NGPTOTG)
    ENDDO

    ! Output fields are simply zero'd
    DO IVAR = 1, SIZE(OUT_VAR_NAMES) - 2
        FIELD = FSET%FIELD(TRIM(OUT_VAR_NAMES(IVAR)))
        CALL FIELD%DATA(TMP3D)
        DO B=1, SELF%NBLOCKS
           TMP3D(:,:,B) = 0.0_JPRB
        END DO
    ENDDO

    FIELD = FSET%FIELD(TRIM("PRAINFRAC_TOPRFZ"))
    CALL FIELD%DATA(TMP2D)
    DO B=1, SELF%NBLOCKS
       TMP2D(:,B) = 0.0_JPRB
    END DO

    FIELD = FSET%FIELD(TRIM("TENDENCY_LOC_CLD"))
    CALL FIELD%DATA(TMP4D)
    DO B=1, SELF%NBLOCKS
       TMP4D(:,:,:,B) = 0.0_JPRB
    END DO

    ! Initialize global parameters from the input file
    CALL LOAD_SCALAR('PTSPHY', SELF%PTSPHY)
    CALL LOAD_SCALAR('LDSLPHY', SELF%LDSLPHY)
    CALL LOAD_SCALAR('LDMAINCALL', SELF%LDMAINCALL)
    CALL YOMCST_LOAD_PARAMETERS()
    CALL YOETHF_LOAD_PARAMETERS()
    CALL YRECLDP_LOAD_PARAMETERS()
    CALL YREPHLI_LOAD_PARAMETERS()

    CALL INPUT_FINALIZE()

    CALL FIELD%FINAL()
    CALL GRID%FINAL()
    CALL TRACE%FINAL()
  END SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_LOAD


  SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_VALIDATE(SELF, FSET, FSPACE, NGPTOTG)
    ! Validate the correctness of output against reference data
    CLASS(CLOUDSC_GLOBAL_ATLAS_STATE) :: SELF
    TYPE(ATLAS_FIELDSET), INTENT(INOUT) :: FSET
    TYPE(ATLAS_FUNCTIONSPACE_BLOCKSTRUCTUREDCOLUMNS), INTENT(IN) :: FSPACE
    INTEGER(KIND=JPIM), INTENT(IN) :: NGPTOTG

    TYPE(ATLAS_TRACE) :: TRACE
    INTEGER(KIND=JPIM) :: KLON, IVAR

    TRACE = ATLAS_TRACE("cloudsc_global_atlas_state_mod.F90", __LINE__, "VALIDATE", "VALIDATE")
    CALL INPUT_INITIALIZE(NAME='reference')
    CALL LOAD_SCALAR('KLON', KLON)
    WRITE(0,*) "KLON = ", KLON

    ! Write variable validation header
    IF (IRANK == 0) THEN
      print '(1X,A20,1X,A3,5(1X,A20))', &
           & 'Variable','Dim', 'MinValue','MaxValue','AbsMaxErr','AvgAbsErr/GP','MaxRelErr-%'
    END IF

    ! Actual variable validation
    CALL VALIDATEVAR_ATLAS(FSET, FSPACE, 'PLUDE', KLON, NGPTOTG)
    DO IVAR = 1, SIZE(OUT_VAR_NAMES)
        CALL VALIDATEVAR_ATLAS(FSET, FSPACE, TRIM(OUT_VAR_NAMES(IVAR)), KLON, NGPTOTG)
    ENDDO
    CALL INPUT_FINALIZE()
    CALL TRACE%FINAL()
  END SUBROUTINE CLOUDSC_GLOBAL_ATLAS_STATE_VALIDATE


END MODULE CLOUDSC_GLOBAL_ATLAS_STATE_MOD
